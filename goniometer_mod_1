(C) 2007, Michael Gruhn.

desc:Goniometer
desc:Goniometer [LOSER]
//tags: analysis visualization
//author: LOSER
//slider1:0<0,2,1{Dots,Lines,Rays}>Display

in_pin:left input
in_pin:right input
out_pin:none
options:no_meter
options:gfx_hz=12

@init
gfx_clear=0;
off = srate;
rot=-45*0.017453292;
DOTS = 1;
LINES = 0;
RAYS = 0;
MAXDRAWSPLS = srate*2;
PHASEUPDATERATE = -1;
n = 0;
phC = 0;
ph = 0;
@slider

@block

@sample
s0 = sign(spl0);
s1 = sign(spl1);

angle = atan( spl0 / spl1 );
(s0 == 1 && s1 == -1) || (s0 == -1 && s1 == -1) ? angle += 3.141592654;
s0 == -1 && s1 == 1 ? angle += 6.283185307;
spl1 == 0 ? spl0 > 0 ? angle = 1.570796327 : angle = 4.71238898;
spl0 == 0 ? spl1 > 0 ? angle = 0 : angle = 3.141592654;
radius = sqrt( sqr(spl0)+sqr(spl1) ) ;
angle -= rot;
vert = 0[b] = cos(angle)*radius;
horiz = off[b] = sin(angle)*radius;
b<MAXDRAWSPLS ? b += 1;


s0 != s1 ? phC-=1:phC+=1;
(n+=1) > PHASEUPDATERATE ? (
  ph = (1-(phC/n))/2;
  phC = n = 0;
);

@gfx 1080 1080

size = min(gfx_w,gfx_h*0.95)|0;

// override drawing functions to center
gxo = gfx_w*.5 - size*.5;
function gfx_lineto(x,y,aa) ( gfx_x+=gxo; gfx_lineto(x+gxo,y,aa); gfx_x-=gxo; );
function gfx_setpixel(r,g,b) ( gfx_x+=gxo; gfx_setpixel(r,g,b); gfx_x-=gxo; );
function gfx_drawnumber(y,x) ( gfx_x+=gxo; gfx_w<200||gfx_drawnumber(y,x); gfx_x-=gxo; );
function gfx_drawchar(x) (gfx_x+=gxo; gfx_w<200||gfx_drawchar(x); gfx_x-=gxo;);

sizeH = size/2;
sizeDSqr05 = sizeH * 0.70710681;
sizeQ = sizeH/2;
size3Q = 3*sizeQ;

gfx_r=gfx_g=gfx_b=0; gfx_a=0.025;
gfx_x=gfx_y=0;
gfx_rectto(gfx_w,gfx_h);

x = y = sizeH;

// DRAWING HAPPENS HERE!

_r = 0.35 + s1*0.1;
_g = 0.75 + s1*0.1;
_b = 0 + s1*0.11;

_gfx_x = 0;
_gfx_y = 0;

// CHANGE MAXDRAWSPLS FOR MORE PIXELS!

// TO DO: MULTIPLY ALPHA OF SOME PIXELS BY SIDECHAIN INPUT WHICH COULD BE DRY OR WET SIGNAL!
i = min(b,MAXDRAWSPLS);
while (

  _gfx_x = sizeH+0[i]*sizeDSqr05;
  _gfx_y = 1.1*sizeH-off[i]*sizeDSqr05;
  
  gfx_a=1;
  
  gfx_x= _gfx_x;
  gfx_y= _gfx_y;
  gfx_setpixel(_r,_g,_b);
  
  gfx_x= _gfx_x;
  gfx_y= _gfx_y + 1;
  gfx_setpixel(_r,_g,_b);
  
  gfx_x= _gfx_x - 1;
  gfx_y= _gfx_y;
  gfx_setpixel(_r,_g,_b);
  
  (i-=1)>0;
);
b=0;


gfx_r=gfx_g=gfx_b=0.5; gfx_a=0;

gfx_x=sizeQ; gfx_y=sizeQ;
gfx_lineto(size3Q,size3Q,0);

gfx_x=sizeQ; gfx_y=size3Q;
gfx_lineto(size3Q,sizeQ,0);
